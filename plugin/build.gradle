plugins {
    id 'java-gradle-plugin'
    id 'maven-publish'
}

group = 'org.gradlex'
version = '0.0.1'

repositories {
    mavenCentral()
}

dependencies {
    implementation project(':lib')
    testImplementation 'commons-io:commons-io:1.4'
    testImplementation 'commons-lang:commons-lang:2.6'
    testImplementation 'org.junit.jupiter:junit-jupiter:5.8.1'
}

gradlePlugin {
    plugins {
        greeting {
            id = 'org.gradlex.templates'
            implementationClass = 'org.gradlex.templates.GradleTemplatesPlugin'
        }
    }
}

sourceSets {
    functionalTest {
    }
}

tasks.named('test') {
    // Use JUnit Jupiter for unit tests.
    useJUnitPlatform()
}

configurations.functionalTestImplementation.extendsFrom(configurations.testImplementation)

String localRepoName = 'Mylocal'

publishing {
    repositories {
        maven {
            name = localRepoName
            // change to point to your repo, e.g. http://my.org/repo
            url = rootProject.layout.buildDirectory.dir('repo')
        }
    }
}

tasks.register('functionalTest', Test) {
    dependsOn ":plugin:publishAllPublicationsTo${localRepoName}Repository"
    dependsOn ":lib:publishAllPublicationsTo${localRepoName}Repository"

    jvmArgs('-DtestRootDir=' + project.layout.buildDirectory.dir('functionalTest').get().asFile,
            "-DpluginRepo=" + project.rootProject.layout.buildDirectory.dir('repo').get().asFile)
    testClassesDirs = sourceSets.functionalTest.output.classesDirs
    classpath = sourceSets.functionalTest.runtimeClasspath
    useJUnitPlatform()
}

gradlePlugin.testSourceSets(sourceSets.functionalTest)

tasks.named('check') {
    // Run the functional tests as part of `check`
    dependsOn(tasks.functionalTest)
}
