plugins {
    id 'java-gradle-plugin'
    id 'maven-publish'
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.freemarker:freemarker:2.3.31'
    implementation 'org.eclipse.jgit:org.eclipse.jgit:5.7.0.202003110725-r'
    implementation 'commons-io:commons-io:1.4'
    implementation('com.fasterxml.jackson.core:jackson-core:2.13.3')
    implementation('com.fasterxml.jackson.core:jackson-annotations:2.13.3')
    implementation('com.fasterxml.jackson.core:jackson-databind:2.13.3')
    testImplementation 'org.junit.jupiter:junit-jupiter:5.8.1'
}

gradlePlugin {
    plugins {
        greeting {
            id = 'org.gradlex.archetypes.greeting'
            implementationClass = 'org.gradlex.archetypes.GradleArchetypesPlugin'
        }
    }
}

// Add a source set for the functional test suite
sourceSets {
    functionalTest {
    }
}

configurations.functionalTestImplementation.extendsFrom(configurations.testImplementation)

// Add a task to run the functional tests
tasks.register('functionalTest', Test) {
    jvmArgs('-DtestRootDir=' + project.layout.buildDirectory.dir('functionalTest').get().asFile)
    testClassesDirs = sourceSets.functionalTest.output.classesDirs
    classpath = sourceSets.functionalTest.runtimeClasspath
    useJUnitPlatform()
}

gradlePlugin.testSourceSets(sourceSets.functionalTest)

tasks.named('check') {
    // Run the functional tests as part of `check`
    dependsOn(tasks.functionalTest)
}

tasks.named('test') {
    // Use JUnit Jupiter for unit tests.
    useJUnitPlatform()
}


String localRepoName = 'Mylocal'
publishing {
    repositories {
        maven {
            name = localRepoName
            // change to point to your repo, e.g. http://my.org/repo
            url = layout.buildDirectory.dir('repo')
        }
    }
}

group = 'org.gradlex'
version = '0.0.1'

tasks.named('functionalTest') {Test test ->
    dependsOn "publishAllPublicationsTo${localRepoName}Repository"
    test.jvmArgs("-DpluginRepo=" + layout.buildDirectory.dir('repo').get().asFile)
}

